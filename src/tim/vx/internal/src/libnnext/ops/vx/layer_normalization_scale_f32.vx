#include "cl_viv_vx_ext.h"

/**************************layernorm float16***********************************/
_viv_uniform int width;
_viv_uniform float dimRatio;
_viv_uniform VXC_512Bits uniFp16SumSqr_dp8x2;
_viv_uniform VXC_512Bits UniFP16toFP32Lo4_dp4x4;
_viv_uniform VXC_512Bits uniExtractHalf4_dp4x4;

__kernel void layer_norm_F16F32toF16(
    image2d_array_t input, image2d_t bias, image2d_t scale,
    image2d_array_t output, float eps)
{
    int4 coord = (int4)(0, get_global_id(1), get_global_id(2), 0);
    int4 coord_out = coord;

    int8 input_desc, output_desc;
    _viv_asm(COPY, input_desc, input, sizeof(input_desc));
    int baseAddr_a = (int)get_global_id(2) * input_desc.s4 + input_desc.s0;
    _viv_asm(MOV, coord.z, baseAddr_a);

    vxc_short8 src0;
    vxc_float sum = 0, sqr = 0;
    VXC_OP4(img_load_3d, src0, input, coord.xyzz, VXC_5BITOFFSET_XY(0, 0), \
                    VXC_MODIFIER(0, 7, 0, VXC_RM_TowardZero, 0));
    Image img1 = create_image_from_image2d(bias, 4);
    Image img2 = create_image_from_image2d(scale, 4);

    _viv_asm(COPY, output_desc, output, sizeof(output_desc));
    int baseAddr = (int)get_global_id(2) * output_desc.s4 + output_desc.s0;
    _viv_asm(MOV, coord_out.w, baseAddr);

    for(coord.x = 8; coord.x < (width+8); coord.x += 8)
    {
        vxc_half8  val0_h;
        _viv_asm(COPY, val0_h, src0, 16);
        VXC_OP4(img_load_3d, src0, input, coord.xyzz, VXC_5BITOFFSET_XY(0, 0), \
                    VXC_MODIFIER(0, 7, 0, VXC_RM_TowardZero, 0));
        vxc_float4 sumsqr;
        VXC_DP8x2(sumsqr, val0_h, val0_h, VXC_MODIFIER(0, 1, 0, VXC_RM_TowardZero, 0),\
            uniFp16SumSqr_dp8x2);
        sum += sumsqr.x;
        sqr += sumsqr.y;
    }
    vxc_float mean;
    mean = sum * dimRatio;
    vxc_float vari;
    vari = sqr*dimRatio - mean*mean;
    vari += eps;
    vari = rsqrt(vari);
    vxc_float4 bias_f, scale_f, in_f;
    __global float* bias_ptr = (__global float*)get_image_ptr_from_coord(img1, coord.ww);
    __global float* scale_ptr = (__global float*)get_image_ptr_from_coord(img2, coord.ww);
    for(coord.x = 0; coord.x < width; coord.x += 4)
    {
        VXC_OP4(img_load_3d, src0, input, coord.xyzz, VXC_5BITOFFSET_XY(0, 0), \
                    VXC_MODIFIER(0, 3, 0, VXC_RM_TowardZero, 0));
        bias_f = vload4(0, bias_ptr + coord.x);
        scale_f = vload4(0, scale_ptr + coord.x);
        vxc_half8 in_h;
        _viv_asm(COPY, in_h, src0, 16);
        VXC_DP4x4(in_f, in_h, in_h, VXC_MODIFIER(0, 3, 0, VXC_RM_TowardZero, 0),\
            UniFP16toFP32Lo4_dp4x4);
        vxc_float4 sub, norm;
        sub = in_f - mean;
        norm = scale_f * vari * sub + bias_f;
        half4 norm_h;
        _viv_asm(CONV, norm_h, norm);
        vxc_half8 dst;
        VXC_DP4x4(dst, norm_h, norm_h, VXC_MODIFIER(0, 3, 0, VXC_RM_TowardZero, 0),\
            uniExtractHalf4_dp4x4);
        vxc_short8 dstval;
        _viv_asm(COPY, dstval, dst, 16);
        coord_out.x = coord.x;
        VXC_OP4_NoDest(img_store_3d, output, coord_out.xyww, dstval, \
                VXC_MODIFIER(0, 3, 0,VXC_RM_TowardZero, 0));
    }
}
/*****************************layernorm uint8 to uint8****************************/
_viv_uniform VXC_512Bits uniConvert1stUint8SubZpToFp32_4x4;
_viv_uniform VXC_512Bits uniConvert2ndUint8SubZpToFp32_4x4;
_viv_uniform VXC_512Bits uniConvert3rdUint8SubZpToFp32_4x4;
_viv_uniform VXC_512Bits uniConvert4thUint8SubZpToFp32_4x4;
_viv_uniform VXC_512Bits uniSumU8_16x1;
_viv_uniform VXC_512Bits uniSqrSum_16x1;
_viv_uniform float input_scale;
_viv_uniform int inputZP;
_viv_uniform float outputScale;
_viv_uniform float output_zp;
_viv_uniform int sumInZp;
_viv_uniform int tmpZp1;
_viv_uniform int tmpZp2;
_viv_uniform float e2InScale;
_viv_uniform VXC_512Bits uniConvertInt32toUint8_2x8;

_viv_uniform VXC_512Bits uniInt16SumSqr_dp8x2;
_viv_uniform float dimRatio_scale;

__kernel void layer_norm_U8F32toU8(
    image2d_array_t input, image2d_t bias, image2d_t scale,
    image2d_array_t output, float eps)
{
    int4 coord = (int4)(0, get_global_id(1), get_global_id(2), 0);
    int4 coord_out = coord;

    vxc_uchar16 src0, src2;
    float sum = 0, sqr = 0;
    vxc_float4 bias_f0, bias_f1, bias_f2, bias_f3, scale_f0, scale_f1, scale_f2, scale_f3;
    int tmpSum = 0, tmpSqr = 0;
    vxc_int4 tmpSum1;
    vxc_int4 tmpSqr1;
    short zp = inputZP;

    int8 input_desc, output_desc;
    _viv_asm(COPY, input_desc, input, sizeof(input_desc));
    int baseAddr_a = (int)get_global_id(2) * input_desc.s4 + input_desc.s0;
    _viv_asm(MOV, coord.z, baseAddr_a);

    _viv_asm(COPY, output_desc, output, sizeof(output_desc));
    int baseAddr = (int)get_global_id(2) * output_desc.s4 + output_desc.s0;
    _viv_asm(MOV, coord_out.w, baseAddr);

    for(coord.x = 0; coord.x < width; coord.x += 16)
    {
        VXC_OP4(img_load_3d, src0, input, coord.xyzz, VXC_5BITOFFSET_XY(0, 0), \
                    VXC_MODIFIER(0, 15, 0, VXC_RM_TowardZero, 0));
        VXC_DP16x1(tmpSum1, src0, src0, VXC_MODIFIER(0, 0, 0, VXC_RM_TowardZero, 0), uniSumU8_16x1);
        tmpSum += (tmpSum1.x);
        VXC_DP16x1(tmpSqr1, src0, src0, VXC_MODIFIER(0, 0, 0, VXC_RM_TowardZero, 0), uniSqrSum_16x1);
        tmpSqr += (tmpSqr1.x + tmpZp1 * tmpSum1.x);
    }
    sum = (tmpSum + sumInZp) * input_scale;
    sqr = (tmpSqr + tmpZp2) * e2InScale;

    float mean, vari;
    mean = sum * dimRatio;
    vari = sqr*dimRatio - mean*mean;
    vari += eps;
    vari = rsqrt(vari);
    vxc_int4 tmpVal0, tmpVal1;
    vxc_float4  tmpData0, tmpData1, tmpData2, tmpData3;

    Image img1 = create_image_from_image2d(bias, 4);
    Image img2 = create_image_from_image2d(scale, 4);
    __global float* bias_ptr = (__global float*)get_image_ptr_from_coord(img1, coord.ww);
    __global float* scale_ptr = (__global float*)get_image_ptr_from_coord(img2, coord.ww);
    for(coord.x = 0; coord.x < width; coord.x += 16)
    {
        VXC_OP4(img_load_3d, src0, input, coord.xyzz, VXC_5BITOFFSET_XY(0, 0), \
                    VXC_MODIFIER(0, 15, 0, VXC_RM_TowardZero, 0));
        bias_f0 = vload4(0, bias_ptr);
        bias_f1 = vload4(1, bias_ptr);
        bias_f2 = vload4(2, bias_ptr);
        bias_f3 = vload4(3, bias_ptr);
        scale_f0 = vload4(0, scale_ptr);
        scale_f1 = vload4(1, scale_ptr);
        scale_f2 = vload4(2, scale_ptr);
        scale_f3 = vload4(3, scale_ptr);
        bias_ptr += 16;
        scale_ptr += 16;

        VXC_DP4x4(tmpData0, src0, zp, VXC_MODIFIER(0, 3, 0, VXC_RM_TowardZero, 0),\
            uniConvert1stUint8SubZpToFp32_4x4);
        VXC_DP4x4(tmpData1, src0, zp, VXC_MODIFIER(0, 3, 0, VXC_RM_TowardZero, 0),\
            uniConvert2ndUint8SubZpToFp32_4x4);
        VXC_DP4x4(tmpData2, src0, zp, VXC_MODIFIER(0, 3, 0, VXC_RM_TowardZero, 0),\
            uniConvert3rdUint8SubZpToFp32_4x4);
        VXC_DP4x4(tmpData3, src0, zp, VXC_MODIFIER(0, 3, 0, VXC_RM_TowardZero, 0),\
            uniConvert4thUint8SubZpToFp32_4x4);
        tmpData0 *= input_scale;
        tmpData1 *= input_scale;
        tmpData2 *= input_scale;
        tmpData3 *= input_scale;

        vxc_float4 norm;
        tmpData0 -= mean;
        norm = scale_f0 * vari * tmpData0 + bias_f0;
        tmpVal0 = convert_int4_rte(norm * outputScale + output_zp);

        tmpData1 -= mean;
        norm = scale_f1 * vari * tmpData1 + bias_f1;
        tmpVal1 = convert_int4_rte(norm * outputScale + output_zp);
        VXC_DP2x8(src2, tmpVal0, tmpVal1, VXC_MODIFIER(0, 7, 0, VXC_RM_TowardZero, 1),\
            uniConvertInt32toUint8_2x8);

        tmpData2 -= mean;
        norm = scale_f2 * vari * tmpData2 + bias_f2;
        tmpVal0 = convert_int4_rte(norm * outputScale + output_zp);

        tmpData3 -= mean;
        norm = scale_f3 * vari * tmpData3 + bias_f3;
        tmpVal1 = convert_int4_rte(norm * outputScale + output_zp);
        VXC_DP2x8(src2, tmpVal0, tmpVal1, VXC_MODIFIER(8, 15, 0, VXC_RM_TowardZero, 1),\
            uniConvertInt32toUint8_2x8);
        coord_out.x = coord.x;
        VXC_OP4_NoDest(img_store_3d, output, coord_out.xyww, src2, \
                VXC_MODIFIER(0, 15, 0,VXC_RM_TowardZero, 0));
    }
}

__kernel void layer_norm_I16F32toI16(
    image2d_array_t input, image2d_t bias, image2d_t scale,
    image2d_array_t output, float eps)
{
    int4 coord = (int4)(0, get_global_id(1), get_global_id(2), 0);

    int8 input_desc, output_desc;
    _viv_asm(COPY, input_desc, input, sizeof(input_desc));
    int baseAddr_a = (int)get_global_id(2) * input_desc.s4 + input_desc.s0;
    _viv_asm(MOV, coord.z, baseAddr_a);

    _viv_asm(COPY, output_desc, output, sizeof(output_desc));
    int baseAddr = (int)get_global_id(2) * output_desc.s4 + output_desc.s0;
    _viv_asm(MOV, coord.w, baseAddr);

    vxc_short8 src0, dst;
    vxc_float sum = 0, sqr = 0;
    for(; coord.x < width;)
    {
        VXC_OP4(img_load_3d, src0, input, coord.xyzz, VXC_5BITOFFSET_XY(0, 0), \
                    VXC_MODIFIER(0, 7, 0, VXC_RM_TowardZero, 0));
        coord.x += 8;
        vxc_float4 sumsqr;
        VXC_DP8x2(sumsqr, src0, src0, VXC_MODIFIER(0, 1, 0, VXC_RM_TowardZero, 0),\
                    uniInt16SumSqr_dp8x2);
        sum += sumsqr.x;
        sqr = sqr + sumsqr.y * e2InScale;
    }
    vxc_float mean;
    mean = sum * dimRatio_scale;
    vxc_float vari;
    vari = sqr*dimRatio - mean*mean;
    vari += eps;
    vari = rsqrt(vari);

    short zp = inputZP;
    vxc_float4  tmpData0, tmpData1;
    vxc_float4 bias_f0, bias_f1, scale_f0, scale_f1;
    vxc_int4 tmpVal0, tmpVal1;

    int2 coord_bias = (int2)(0, 0);
    Image img1 = create_image_from_image2d(bias, 4);
    Image img2 = create_image_from_image2d(scale, 4);
    __global float* bias_ptr = (__global float*)get_image_ptr_from_coord(img1, coord_bias);
    __global float* scale_ptr = (__global float*)get_image_ptr_from_coord(img2, coord_bias);
    for(coord.x = 0; coord.x < width; coord.x += 8)
    {
        VXC_OP4(img_load_3d, src0, input, coord.xyzz, VXC_5BITOFFSET_XY(0, 0), \
                    VXC_MODIFIER(0, 7, 0, VXC_RM_TowardZero, 0));
        bias_f0 = vload4(0, bias_ptr);
        bias_f1 = vload4(1, bias_ptr);
        scale_f0 = vload4(0, scale_ptr);
        scale_f1 = vload4(1, scale_ptr);
        bias_ptr += 8;
        scale_ptr += 8;

        VXC_DP4x4(tmpData0, src0, zp, VXC_MODIFIER(0, 3, 0, VXC_RM_TowardZero, 0),\
                uniConvert1stUint8SubZpToFp32_4x4);
        VXC_DP4x4(tmpData1, src0, zp, VXC_MODIFIER(0, 3, 0, VXC_RM_TowardZero, 0),\
                uniConvert2ndUint8SubZpToFp32_4x4);

        vxc_float4 sub, norm;
        sub = tmpData0 * input_scale - mean;
        norm = scale_f0 * vari * sub + bias_f0;
        tmpVal0 = convert_int4_rte(norm * outputScale + output_zp);
        sub = tmpData1 * input_scale - mean;
        norm = scale_f1 * vari * sub + bias_f1;
        tmpVal1 = convert_int4_rte(norm * outputScale + output_zp);

        VXC_DP2x8(dst, tmpVal0, tmpVal1, VXC_MODIFIER(0, 7, 0, VXC_RM_TowardZero, 1),\
                uniConvertInt32toUint8_2x8);
        VXC_OP4_NoDest(img_store_3d, output, coord.xyww, dst, \
                VXC_MODIFIER(0, 7, 0,VXC_RM_TowardZero, 0));
    }
}